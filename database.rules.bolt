isAdmin() {
    auth != null
        && root.child("users").child(auth.uid).child("isAdmin").val() == true
}

isMine(uid) {
    auth != null && auth.uid == uid
}

createOnly(value) {
    prior(value) == null && value != null
}

path /users {
    read() { isAdmin() }
    write() { isAdmin() }
}
path /users/{uid} {
    read() { true }
}

path /state {
    read() { true }
    write() { isAdmin() }
}

path /admin {
    read() { isAdmin() }
    write() { isAdmin() }
}

path /admin/games/{game}/questions/{question} is Question {}
path /admin/games/{game}/questions/{question}/answers/{answer} is Answer {}

path /answers {
    read() { isAdmin() }
    write() { isAdmin() }
}

path /answers/{game}/{question}/{uid} is Submission {
    read() { isAdmin() || isMine(uid) }
    write() {
        isAdmin() ||
        (
            isMine(uid)
            && createOnly(this)
            && root.child("state").child("kind").val() == "questionOpen"
            && root.child("state").child("gameId").val() == game
            && root.child("state").child("questionId").val() == question
        )
    }
}

path /ranking {
    read() { true }
    write() { isAdmin() }
}

path /ranking/{game}/{question}/ranking/{index} is Ranking {}

path /images/{image} is Image {
    read() { true }
    write() { isAdmin() }
}

type Submission {
    validate() { this.ts == now }

    ts: Number,
    answerId: String
}

type Question {
    validate() { newData.hasChildren(['id']) && newData.child("id").isString() }
}

type Answer {
    id: String,
    text: String
}

type Ranking {
    uid: String,
    rank: Number | Null,
    score: Number,
    delta: Number,
    name: String,
    isHidden: Boolean | Null
}

type Image {
    url: String
}
